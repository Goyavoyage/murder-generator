
category Money
begin
  translation fr "Finance"
  translation en "Money"
  translation eo "Monaĵoj"
end

declare event MoneyExchange
begin
  event Personal
  category Money
end

declare attribute Richness
begin
  category Money

  translation fr "Richesse"
  translation en "Wealth"
  translation eo "Riĉeco"
end

attribute Richness Rich
begin
  translation fr "riche"
  translation en "wealthy"
  translation eo "riĉa"

  translation fr:adj "riche"
  translation en:adj "wealthy"
  translation eo:adj "riĉa"
end

attribute Richness Neutral
begin
  translation fr "neutre"
  translation en "neutral"
  translation eo "neŭtrala"
end

attribute Richness Poor
begin
  translation fr "pauvre"
  translation en "poor"
  translation eo "malriĉa"

  translation fr:adj "pauvre"
  translation en:adj "poor"
  translation eo:adj "malriĉa"
end

element Heritage
begin
  let P be player
  let any other player be with contact FamilyMember to P not as Parent or Sibling

  provide short event to P
  begin
    event ParentDeath
    assume no event ParentDeath to P before

    provide compatible attribute Richness to P as Rich

    translation fr "Mort des parents de " P "."
                   "  Ils " P:+pro:+cli " laissent une importante fortune."
    translation en "Death of " P "’s parents."
                   "  They leave " P:+pro:+cli " with an important wealth."
    translation eo "Morto de la gepatroj de " P "."
                   "  Ili forlasas " P:+pro:+acc " kun granda heredaĵo."
  end
end

element SharedHeritage
begin
  let A be player with contact FamilyMember to B as Sibling
  let B be player with contact FamilyMember to A as Sibling
  let any other player be with contact FamilyMember to A not as Parent or Sibling
                          with contact FamilyMember to B not as Parent or Sibling

  provide short event to A and B
  begin
    event ParentDeath
    assume no event ParentDeath to A or B before

    provide compatible attribute Richness to A as Rich
    provide compatible attribute Richness to B as Rich

    translation fr "Mort des parents de " A " et " B "."
                   "  Ils leur laissent une importante fortune."
    translation en "Death of " A " and " B "’s parents."
                   "  They leave them with an important wealth."
    translation eo "Morto de la gepatroj de " A " kaj " B "."
                   "  Ili forlasas ilin kun granda heredaĵo."
  end
end

element DwarfFocusedTheirLifeAtAccumulatingGold
begin
  let P be player with attribute Specy as Dwarf

  provide long event to P
  begin
    event Work

    provide compatible attribute Richness to P as Rich

    translation fr P:+sbeg " a petit à petit accumulé de grandes richesses."
    translation en P:+sbeg " accumulated a large wealth step after step."
    translation eo P:+sbeg " iom post iom amasiĝis grandan kvanton da mono."
  end
end


declare contact FinancialDebt
begin
  category Money

  translation fr "Dette"
  translation en "Debt"
  translation eo "Ŝuldo"
end

contact FinancialDebt None
begin
  translation fr "aucune"
  translation en "none"
  translation eo "nenia"
end

contact FinancialDebt Small
begin
  translation fr "faible"
  translation en "small"
  translation eo "malalta"

  translation fr:adj "faible"
  translation en:adj "small"
  translation eo:adj "malalta"
end

contact FinancialDebt Large
begin
  translation fr "importante"
  translation en "large"
  translation eo "alta"

  translation fr:adj:fem "importante"
  translation fr:adj:mas "important"
  translation en:adj "large"
  translation eo:adj "alta"
end

contact FinancialDebt Old
begin
  (* Large but from the old times: it would be awkward to require it now. *)
  translation fr "ancienne"
  translation en "old"
  translation eo "malnova"

  translation fr:adj:fem "vielle"
  translation fr:adj:mas "vieux"
  translation en:adj "old"
  translation eo:adj "malnova"
end

element CausalDebt
begin
  let A be player with attribute Richness as Neutral
                  with contact Know to B as True
  let B be player with attribute Richness as Neutral
                  with contact Know to A as True

  provide very short event to A and B
  begin
    event MoneyExchange

    provide compatible contact FinancialDebt from A to B as Small

    translation fr A:+sbeg " a demandé de l’argent à " B "."
                   "  " B:+sbeg:+pro " " A:+pro:+cli " en a donné."
    translation en A:+sbeg " asked money to " B "."
                   "  " B:+sbeg:+pro " gave some to " A:+pro:+cli "."
    translation eo A:+sbeg " petis monon al " B "."
                   "  " B:+sbeg:+pro " al " A:+pro " donis."
  end

  provide relation between A and B as asymmetrical trust neutral
end

element SmallDebtNeutralPoor
begin
  let A be player with attribute Richness as Poor
  let B be player with attribute Richness as Neutral
                  with contact Know to A as True

  provide very short event to A and B
  begin
    event MoneyExchange

    provide compatible contact FinancialDebt from A to B as Small

    translation fr A:+sbeg " a demandé de l’argent à " B "."
                   "  Vu le besoin, " B:+pro " " A:+pro:+cli " en a donné."
    translation en A:+sbeg " asked money to " B "."
                   "  Given the circumstances, " B:+pro " gave some to " A:+pro:+cli "."
    translation eo A:+sbeg " petis monon al " B "."
                   "  Komprenante la bezonon, " B:+pro " al " A:+pro " donis."
  end

  provide relation between A and B as asymmetrical avoidance undetermined
  provide compatible contact Know from A to B as True
end

element RichPaysItsDebt
begin
  let A be player with attribute Richness as Rich
                  with contact Know to B as True
  let B be player

  provide contact FinancialDebt from A to B as Large

  provide relation between A and B as asymmetrical neutral undetermined
  provide compatible contact Know from B to A as True

  (* TODO: provide event to A and B *)
end

element TookMoneyToBuildAShopAndIsNowRich
begin
  let A be player with attribute Richness as Neutral
  let B be player

  provide very short event to A and B
  begin
    event MoneyExchange

    provide contact FinancialDebt from B to A as Old

    translation fr A:+sbeg " a demandé de l’argent à " B " pour monter son entreprise."
                   "  " B:+sbeg:+pro " " A:+pro:+cli " en a donné."
    translation en A:+sbeg " asked money to " B " to set up a shop."
                   "  " B:+sbeg:+pro " gave some to " A:+pro:+cli "."
    translation eo A:+sbeg " petis monon al " B " por komenci ties komercon."
                   "  " B:+sbeg:+pro " al " A:+pro " donis."
  end

  (* TODO: Put the as an assumption of this event, to enable another
     event to properly define it.
     Then add the assumption that the next event is after an event providing
     the Job attribute (and that the first event was before). *)
  provide attribute Job to B as Seller

  provide long event to B
  begin
    event Personal
    assume no event providing attribute Richness to B before

    provide attribute Richness to B as Rich

    translation fr "Le succès du commerce de " B " l’a rendu riche."
    translation en B:+sbeg "’s shop was quite successful and made " B:+pro:+cli " wealthy."
    translation eo "La komerco de " B " sukcesis kaj riĉigis " B:+pro:+acc "."
  end

  provide relation between B and A as asymmetrical trust undetermined
  provide compatible contact Know between B and A as True
end

element TookMoneyToBuildAShopButGotBankrupt
begin
  let A be player with attribute Richness as Neutral
  let B be player

  provide very short event to A and B
  begin
    event MoneyExchange

    provide contact FinancialDebt from B to A as Large

    translation fr A:+sbeg " a demandé de l’argent à " B " pour monter son entreprise."
                   "  " B:+sbeg:+pro " " A:+pro:+cli " en a donné."
    translation en A:+sbeg " asked money to " B " to set up a shop."
                   "  " B:+sbeg:+pro " gave some to " A:+pro:+cli "."
    translation eo A:+sbeg " petis monon al " B " por komenci ties komercon."
                   "  " B:+sbeg:+pro " al " A:+pro " donis."
  end

  (* TODO: Same as above: put this as an assumption and set up the chronology of events. *)
  provide attribute Job to B as Seller

  provide long event to B
  begin
    event Personal

    provide attribute Richness to B as Poor

    translation fr "Le commerce de " B " n’a pas tenu très longtemps et est maintenant en faillite, entrainant avec lui toutes les économies de " B "."
    translation en B:+sbeg "’s shop went brankrupt, disappearing with all trace of wealth."
    translation eo "La komerco de " B " fiaskis, puŝante " B:+pro:+acc " malriĉecen."
  end

  provide relation between B and A as asymmetrical avoidance hate
  provide compatible contact Know between B and A as True
end

element ImportantSeller
begin
  let P be player

  (* TODO: Same than above *)
  provide compatible attribute Job to P as Seller

  provide long event to P
  begin
    event Work

    provide compatible attribute Richness to P as Rich

    translation fr P:+sbeg:mas " a longuement travaillé pour devenir un marchand réputé et fortuné."
    translation fr P:+sbeg:mas " a longuement travaillé pour devenir une marchande réputée et fortunée."
    translation en P:+sbeg " worked quite a long time to reach this level of wealth and reputation."
    translation eo P:+sbeg " longe laboris por atingi tiun nivelon de riĉeco kaj bonreputacio."
  end
end

element DecidedToDisappearAfterLotsOfDebts
begin
  let A be player with contact FinancialDebt to B as Large
  let B be player

  provide short event to A
  begin
    event Work
    assume event providing contact FinancialDebt from A to B before

    provide attribute Famousness to A as Hiding

    translation fr "Vu l’importance des ses dettes, " A " decidait de disparaître définitivement."
    translation en "Given the reach of debts, " A " decided to definitively disappear from the sight of others."
    translation eo "Pro ties grandegaj ŝuldoj, " A " decidis por ĉiam malaperi."
  end

  add difficulty to A
end


declare contact Favor
begin
  translation fr "Faveur"
  translation en "Favor"
  translation eo "Helpŝuldo"
end

contact Favor Large
begin
  translation fr "grande"
  translation en "large"
  translation eo "granda"

  translation fr:adj:fem "grande"
  translation fr:adj:mas "grand"
  translation en:adj "large"
  translation eo:adj "granda"
end

contact Favor Small
begin
  translation fr "petite"
  translation en "small"
  translation eo "malgranda"

  translation fr:adj:fem "petite"
  translation fr:adj:mas "petit"
  translation en:adj "small"
  translation eo:adj "malgranda"

  compatible with Large
end

element FinancialDebtSmallFavor
begin
  let A be player with contact FinancialDebt to B as Small
  let B be player

  provide compatible contact Favor from A to B as Small
end

element FinancialDebtLargeFavor
begin
  let A be player with contact FinancialDebt to B as Large
  let B be player

  provide compatible contact Favor from A to B as Large
end


