
contact Goal Harm
begin
  (* A player wants to harm as much as possible this other player. *)
  translation fr "faire du mal"
  translation en "harm"
  translation eo "vundi"

  compatible with Kill
end

contact Goal Kill
begin
  (* A player wants to kill this other player. *)
  category NonKids

  translation fr "tuer"
  translation en "kill"
  translation eo "mortigi"
end

category AncestralWar
begin
  category NonKids

  translation fr "Guerres ancestrales"
  translation en "Ancestral wars"
  translation eo "Pramilitoj"
end


element DwarvesAndElvesAvoidEachOther
begin
  let D be player with attribute Specy as Dwarf
  let E be player with attribute Specy as Elf

  provide relation between D and E as avoidance

  provide immediate event to D and E
  begin
    event History
    translation fr "Les nains et les elfes ne se sont jamais mélangés."
                   "  Chacun connait bien sûr l’existence des autres, mais les circonstances n’ont jamais été propices pour des échanges pacifiques."
    translation en "Dwarves and elves never mixed each others."
                   "  Each recognizes the existence of the other, naturally, but the circumstances were never satified for peaceful exchanges to happen."
    translation eo "Gnomoj kaj elfoj neniam miksis unu la alian."
                   "  Ĉiuj scias ke la alia ekzistas, kompreneble, sed neniam okazis kuntekston por ke pacaj interŝanĝoj okazu."
  end

  provide immediate event to D
  begin
    translation fr "On ne peut pas faire confiance à un elfe."
    translation en "One can’t trust an elf."
    translation eo "Oni ne povas fidi elfojn."
  end

  provide immediate event to E
  begin
    translation fr "Les nains ne sont pas dignes de confiance."
    translation en "Dwarves are not trustworthy."
    translation eo "Gnomoj ne fideblas."
  end
end

element ElvesCanBeRacist
begin
  let E be player with attribute Specy as Elf
                  with contact Know to P as False
  let P be player with attribute Specy not as Elf

  provide relation from E to P as avoidance

  (* TODO: Provide historical reasons. *)
end

element HumanBeingsCanBeRacist
begin
  let H be player with attribute Specy as Human
                  with contact Know to P as False
  let P be player with attribute Specy not as Human

  provide relation from H to P as avoidance

  (* TODO: Provide historical reasons. *)
end

element EnemiesOfOurEnemiesAreOurAllies
begin
  let T be player
  let A be player with contact Goal to T as Harm
  let B be player with contact Goal to T as Harm

  (* We provide a trust relation to a player to the other without
   * the players being aware of it: they have to discover this
   * connection themselves. *)
  provide relation from A to B as trust
end

