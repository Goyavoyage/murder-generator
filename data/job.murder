
declare attribute Job
begin
  translation fr "Métier"
  translation en "Job"
  translation eo "Profesio"
end

declare event Work
begin
  event Personal
end

(* * Seller *)

attribute Job Seller
begin
  translation fr "commerçant(e)"
  translation en "seller"
  translation eo "vendisto"

  add translation fr:nom:sin:fem "une commerçante"
  add translation fr:nom:sin:mas "un commerçant"
  add translation fr:nom:def:sin:fem "la commerçante"
  add translation fr:nom:def:sin:mas "le commerçant"
  add translation fr:nom:plu:fem "des commerçantes"
  add translation fr:nom:plu:-fem:+mas "des commerçants"
  add translation fr:nom:plu:fem "des commerçantes"
  add translation fr:nom:def:plu:fem "les commerçantes"
  add translation fr:nom:def:plu:-fem:+mas "les commerçants"
  add translation en:nom:sin "a seller"
  add translation en:nom:plu "sellers"
  add translation en:nom:def:sin "the seller"
  add translation en:nom:def:plu "the sellers"
  add translation eo:nom:sin "komercisto"
  add translation eo:nom:acc:sin "komerciston"
  add translation eo:nom:def:sin "la komercisto"
  add translation eo:nom:def:acc:sin "la komerciston"
  add translation eo:nom:plu "komercistoj"
  add translation eo:nom:acc:plu "komercistojn"
  add translation eo:nom:def:plu "la komercistoj"
  add translation eo:nom:def:acc:plu "la komercistojn"
end

element BecameSellerForHumanContact
begin
  let P be player

  provide event lasting weeks to P
  begin
    event Work

    provide attribute Job to P as Seller

    sentence
    begin
      translation fr P:+sbeg " a toujours aimé le contact humain."
      translation en P:+sbeg " always appreciated human contact."
      translation eo P:+sbeg " ĉiam ŝatus homan kontakton."
    end

    sentence
    begin
      translation fr P:+sbeg:+pro:fem " a donc naturellement décidé de devenir commerçante."
      translation fr P:+sbeg:+pro " a donc naturellement décidé de devenir commerçant."
      translation en "Thus, " P:+pro " naturally decided to be seller."
      translation eo P:+sbeg:+pro " do nature decidis iĝi komercanton."
    end
  end
end

element SellerIsEasilyKnown
begin
  let S be player with attribute Job as Seller
  let P be player

  provide compatible contact Know from P to S as True

  provide event lasting weeks to P
  begin
    event Personal
    assume event providing attribute Job to S before

    translation fr P:+sbeg " va souvent faire ses courses à la boutique de " S "."
    translation en P:+sbeg " frequently shops at " S "’s shop."
    translation eo P:+sbeg " ofte aĉetumas ĉe la vendejo de " S "."
  end
end

element SellerRememberTheirClients
begin
  let S be player with attribute Job as Seller
  let P be player with contact Know to S as True

  provide compatible contact Know from S to P as True

  provide event lasting weeks to S
  begin
    event Work
    assume event providing attribute Job to S before

    translation fr P:+sbeg " va souvent faire ses courses à la boutique de " S "."
    translation en P:+sbeg " frequently shops at " S "’s shop."
    translation eo P:+sbeg " ofte aĉetumas ĉe la vendejo de " S "."
  end
end

(* * Thief *)

attribute Job Thief
begin
  translation fr "voleur(se)"
  translation en "thief"
  translation eo "ŝtelisto"

  add translation fr:nom:sin:fem "une voleuse"
  add translation fr:nom:sin:mas "un voleur"
  add translation fr:nom:def:sin:fem "la voleuse"
  add translation fr:nom:def:sin:mas "le voleur"
  add translation fr:nom:plu:fem "des voleuses"
  add translation fr:nom:plu:-fem:+mas "des voleurs"
  add translation fr:nom:plu:fem "des voleuses"
  add translation fr:nom:def:plu:fem "les voleuses"
  add translation fr:nom:def:plu:-fem:+mas "les voleurs"
  add translation en:nom:sin "a thief"
  add translation en:nom:plu "thieves"
  add translation en:nom:def:sin "the thief"
  add translation en:nom:def:plu "the thieves"
  add translation eo:nom:sin "ŝtelisto"
  add translation eo:nom:acc:sin "ŝteliston"
  add translation eo:nom:def:sin "la ŝtelisto"
  add translation eo:nom:def:acc:sin "la ŝteliston"
  add translation eo:nom:plu "ŝtelistoj"
  add translation eo:nom:acc:plu "ŝtelistojn"
  add translation eo:nom:def:plu "la ŝtelistoj"
  add translation eo:nom:def:acc:plu "la ŝtelistojn"
end

element ThiefBecausePoor
begin
  let P be player with attribute Richness as Poor

  provide event lasting weeks to P
  begin
    event Work
    assume event providing attribute Richness to P before

    provide attribute Job to P as Thief

    translation fr "Étant dans le besoin, " P " a décidé de se faire voleur professionel."
    translation fr "Étant dans le besoin, " P:fem " a décidé de devenir voleuse professionnelle."
    translation en "Due to the circumstances, " P " chose thief as " P:pos " profession."
    translation eo "Pro la bezono, " P " iĝis ŝtelisto."
  end
end

element ThiefToHelp
begin
  let P be player with attribute Richness as Poor
  let T be player with contact Know to P as Intimate
                  with attribute Richness not as Rich

  provide event lasting days to T
  begin
    event Personal
    assume event providing attribute Richness to P before
    assume event providing contact Know from T to P before

    provide attribute Job to T as Thief

    sentence
    begin
      translation fr "Lorsque " T " a appris que " P " était dans une situation précaire, " T:+pro " a été indigné" T:+agr "."
      translation en "When " T " learnt that " P " was in a deeply unstable financiary situation, " T:+pro " was outraged."
      translation eo "Kiam " T " lernis ke " P " estas en malstabileta monsituacio, " T:+pro " furioziĝis."
    end

    sentence
    begin
      translation fr T:+sbeg:+pro " a alors pris une décision qui changera sa vie : dorénavant, " T:+pro " volera aux riches pour donner aux pauvres, et en particulier à " P "."
      translation en T:+sbeg:+pro " then decided once and for all that from now on, " T:+pro " would steal from the rich to give to the poor, and in particular to " P "."
      translation eo T:+sbeg:+pro " tiam firme decidis ke ekde tiam, " T:+pro " ŝtelos riĉulojn por doni al malriĉuloj, kaj specife al " P "."
    end
  end

  remove difficulty to P
end

element ThiefForRevenge
begin
  let R be player
  let P be player with contact FinancialDebt to R as Large
  let T be player with contact Know to P as Intimate
                  with contact Know to R not as Intimate

  provide event lasting days to T
  begin
    event Personal
    assume event providing contact FinancialDebt from P to R before
    assume event providing contact Know from T to P before

    provide attribute Job to T as Thief

    sentence
    begin
      translation fr "Lorsque " T " a appris que " P " devait une importance somme d’argent à " R ", " T:+pro " a été indigné" T:+agr "."
      translation en "When " T " learnt that " P " owes a fortune to " R ", " T:+pro " was outraged."
      translation eo "Kiam " T " lernis ke " P " ŝuldas amason da mono al " R ", " T:+pro " furioziĝis."
    end

    sentence
    begin
      translation fr T:+sbeg:+pro " a alors pris une décision qui changera sa vie : dorénavant, " T:+pro " volera les riches, particulièrement " R "."
      translation en T:+sbeg:+pro " then decided once and for all that from now on, " T:+pro " would steal from the rich, and in particular from " R "."
      translation eo T:+sbeg:+pro " tiam firme decidis ke ekde tiam, " T:+pro " ŝtelos riĉulojn, kaj pli specife " R:+acc "."
    end

    sentence
    begin
      translation fr "C’est depuis sa profession."
      translation en "It has been " T:+pos " job since then."
      translation eo "Ekde tiam, tio iĝis " T:+pos " profesio."
    end
  end

  remove difficulty to P
  add difficulty to R
end

element PoliticalThief
begin
  let T be player

  provide event lasting years to T
  begin
    event Work
    assume event providing attribute Richness to T before

    provide attribute Job to T as Thief

    sentence
    begin
      translation fr T:+sbeg " est voleur professionel."
      translation fr T:+sbeg:fem " est voleuse professionelle."
      translation en T:+sbeg " is a professional thief."
      translation eo T:+sbeg " estas profesie ŝtelisto."
    end

    sentence
    begin
      translation fr "On fait appel à " T:+cli " lorsque l’on cherche à obtenir un objet très précieux et bien gardé."
      translation en T:+sbeg:+pro " is enrolled when one needs a precious object very well guarded."
      translation eo "Oni petas ties servicojn por akiri bonege protektitajn preciozajn objektojn."
    end

    sentence
    begin
      translation fr "Il arrive aussi qu’on ait besoin de ses services pour affaiblir un concurrent commercial ou politique."
      translation en "Sometimes, one needs " T:+pos " services to weaken a competitor, be it commercial or political."
      translation eo "Ankaŭ okazas ke oni bezonu ties servicojn por malfortigi komercan aŭ politikan konkurenton."
    end

    sentence
    begin
      translation fr "Dans tous les cas, " T:+pro " fait payer très cher ses services."
      translation en "Whatever the need, " T:+pro " charges quite a lot for " T:+pos " services."
      translation eo "Ĉiuokaze, " T:+pro " pagigas sufiĉe multe por ties servicoj."
    end
  end

  provide event lasting days to T
  begin
      translation fr "Cela fait quelques temps que " T " n’a pas reçu de commande de clients : il serait temps d’en chercher."
      translation en "It has been some time since " T " got any order from any client: it is now a good time to look for new ones."
      translation eo T:+sbeg " ne ricevis peton de servico: estas nun bona momento por serĉi novan klientojn."
  end
end

(* * Spy *)

(* TODO *)

